// 判断是作为Application来运行还是作为Library来运行,isModule变量来自根目录下的config.gradle文件中,由于项目根目录下的build.gradle文件apply from 了config.gradle。所以子模块可以直接使用
if (isModule) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}

android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion


    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode rootProject.ext.android.versionCode
        versionName rootProject.ext.android.versionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

//        ARouter配置
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }


    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'androidx.appcompat:appcompat:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

//    ARouter 依赖，需要注意的是api要与compiler匹配使用，均使用最新版可以保证兼容
    api rootProject.ext.dependencies["arouter-api"]
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

//    Room库
    api   group: 'androidx.room', name: 'room-runtime', version: rootProject.dependentVersion.roomVersion
    annotationProcessor   group: 'androidx.room', name: 'room-compiler', version: rootProject.dependentVersion.roomVersion
    androidTestImplementation  group: 'androidx.room', name: 'room-testing', version: rootProject.dependentVersion.roomVersion

    implementation 'top.androidman:superbutton:1.1.1'

//    retrofit网络请求框架
    api group: 'com.squareup.retrofit2', name: 'retrofit', version: '2.6.1'

//    KV数据库
    api 'com.snappydb:snappydb-lib:0.5.2'
    api 'com.esotericsoftware.kryo:kryo:2.24.0'

}
